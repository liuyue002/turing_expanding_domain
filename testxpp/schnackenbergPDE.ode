# Schnakenberg RD equation in 1D, discretized 100 pt
# Reaction kinetics
f(u,v)=gamma * (a-u+u^2*v+W)
g(u,v)=gamma * (b-u^2*v)


# Boundary condition (periodic)
# the actual variables ranges 1..199
u0=u99
v0=v99
u100=u1
v100=v1
%[1..99]
u[j]'=f(u[j],v[j]) + (du/h^2)*(u[j+1]+u[j-1]-2*u[j])
v[j]'=g(u[j],v[j]) + (dv/h^2)*(v[j+1]+v[j-1]-2*v[j])
%

# parameters
par a=0.05,b=1.4
par gamma=1,du=1,dv=20
par W=0
par L=100
!h=2*L/100

# initial conditions
init u1=3
init u[2..99]=1.45
init v[1..99]=0.66

# numerics options
@ meth=cvode,bandup=2,bandlo=2,total=2000
@ dt=0.01,nout=100
@ nplot=3,XP=T,YP=u1,YP2=u50,YP3=u99
@ XLO=0,XHI=2000,YLO=0,YHI=4
@ MAXSTOR=2000000
@ nmax=20000,npr=5000,dsmax=0.1,ds=-0.01,dsmin=0.0001,parmin=0,parmax=100
@ autoxmin=0,autoxmax=100,autoymin=0,autoymax=5,autovar=u1

# for array plot: column1=u1 (or v1), ncols=200, row1=0,
# nrows=400,rowskip=100,colskip=2 (skip the v's)
done
